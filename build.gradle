allprojects {
    group 'org.brabocoin'
    version '0.1-SNAPSHOT'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'idea'

    sourceCompatibility = 1.8

    repositories {
        mavenCentral()
        jcenter()
        maven { url 'https://jitpack.io' }
        maven { url 'http://maven.bestsolution.at/efxclipse-releases/' }
    }

    dependencies {
        compile('org.jetbrains:annotations:16.0.2')
        testCompile('org.awaitility:awaitility:3.1.2')
        testCompile('org.junit.jupiter:junit-jupiter-api:5.3.1')
        testCompile('org.junit.jupiter:junit-jupiter-params:5.3.1')
        testRuntime('org.junit.jupiter:junit-jupiter-engine:5.3.1')
    }

    test {
        useJUnitPlatform()
    }

    jar {
        manifest {
            attributes(
                    'Implementation-Version': version,
            )
        }
    }

    task fatJar(type: Jar) {
        manifest.from jar.manifest
        from {
            configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
        } {
            exclude "META-INF/*.SF"
            exclude "META-INF/*.DSA"
            exclude "META-INF/*.RSA"
        }
        with jar
    }
}
