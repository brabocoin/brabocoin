repositories {
    jcenter()
}

dependencies {
    compile('org.controlsfx:controlsfx:8.40.14')
    compile('org.fxmisc.cssfx:cssfx:1.0.0')
    compile('no.tornado:tornadofx:1.7.17')

    compile('com.dlsc.formsfx:formsfx-core:1.3.1')
    compile('at.bestsolution.efxclipse.rt:org.eclipse.fx.ui.controls:3.3.0')

    compile(fileTree('lib') { include '*.jar' })

    compile project(':core')

    testCompile "org.testfx:testfx-core:4.0.15-alpha"
    testCompile "org.testfx:testfx-junit5:4.0.15-alpha"
}

task copyDeploymentIcons(type: Copy) {
    from('src/main/resources/org/brabocoin/brabocoin/gui/icon/') {
        include('48.ico')
        include('48.bmp')
    }
    into('src/main/deploy/package/windows/')

    rename('48.ico', 'Brabocoin.ico')
    rename('48.bmp', 'Brabocoin-setup-icon.bmp')
}

jfx {
    mainClass = 'org.brabocoin.brabocoin.gui.BrabocoinGUI'
    vendor = 'Brabocoin'
    appName = 'Brabocoin'
    jfxMainAppJarName = "brabocoin-" + version + ".jar"
    nativeReleaseVersion = version
    needShortcut = true
    needMenu = true
    additionalAppResources = 'src/main/deploy/package/windows/resources'
    manifestAttributes = [
            'Implementation-Version': version
    ]
}

tasks.jfxNative.dependsOn(copyDeploymentIcons)

task fatCapsule(type: FatCapsule) {
    applicationClass 'org.brabocoin.brabocoin.gui.BrabocoinGUI'
    baseName 'brabocoin'
    classifier null
    reallyExecutable

    capsuleManifest {
        minJavaVersion = '1.8.0'
        applicationId = 'org.brabocoin.gui'
        applicationName = 'Brabocoin'
    }
}

artifacts {
    archives fatCapsule
}
